apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

ext.app = [
        'version'            : '1.2.0',
        'version_code'       : 10,
        'compile_sdk_version': 27,
        'min_sdk_version'    : 21,
        'target_sdk_version' : 27
]

android {
    compileSdkVersion app.compile_sdk_version

    defaultConfig {
        applicationId "com.droidfeed"
        minSdkVersion project.app.min_sdk_version
        targetSdkVersion project.app.target_sdk_version
        versionName project.app.version
        versionCode project.app.version_code
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', "DROIDFEED_GITHUB_URL", "\"https://github.com/dgngulcan/droid-feed\""
        buildConfigField 'String', "DROIDFEED_PRIVACY_POLICY", "\"https://app.termly.io/document/privacy-policy/ab750f11-7f6d-499d-aafb-57697bd8aba0\""

        multiDexEnabled true

        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            shrinkResources false
            minifyEnabled false
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlin_coroutines"
    implementation "androidx.core:core-ktx:$versions.android_ktx"

    implementation "com.android.support:support-fragment:$versions.support_lib"
    implementation "com.android.support:appcompat-v7:$versions.support_lib"
    implementation "com.android.support:support-dynamic-animation:$versions.support_lib"
    implementation "com.android.support:design:$versions.support_lib"
    implementation "com.android.support:cardview-v7:$versions.support_lib"
    implementation "com.android.support:customtabs:$versions.support_lib"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraint_layout"

    implementation "android.arch.lifecycle:runtime:$versions.arch_lifecycle"
    implementation "android.arch.lifecycle:extensions:$versions.arch_lifecycle"
    kapt "android.arch.lifecycle:compiler:$versions.arch_lifecycle"

    implementation "android.arch.persistence.room:runtime:$versions.arch_room"
    kapt "android.arch.persistence.room:compiler:$versions.arch_room"

    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okHttp"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:$versions.retrofit_coroutines_adapter"

    implementation "com.crashlytics.sdk.android:crashlytics:$versions.crashlytics"

    implementation "com.google.firebase:firebase-core:$versions.firebase_core"
    implementation "com.google.firebase:firebase-config:$versions.firebase_config"

    implementation "org.jsoup:jsoup:$versions.jSoup"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:$versions.support_test_runner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.support_test_espresso"
    androidTestImplementation "android.arch.persistence.room:testing:$versions.arch_room"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    androidTestImplementation "org.mockito:mockito-android:$versions.mockito"

}

apply plugin: 'com.google.gms.google-services'